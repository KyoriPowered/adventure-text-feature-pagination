plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'net.kyori.invent' version '1.0.4'
  id 'net.kyori.invent-javadoc-jar' version '1.0.4'
  id 'net.kyori.invent-sonatype-repository' version '1.0.4'
  id 'net.kyori.invent-sources-jar' version '1.0.4'
  id 'net.minecrell.licenser' version '0.4.1'
}

group 'net.kyori'
version '4.0.0-SNAPSHOT'
description 'A pagination feature for Adventure.'

tasks.withType(JavaCompile) {
  options.compilerArgs += ['-Xlint:all']
}

license {
  header rootProject.file('header.txt')
  include '**/*.java'
  newLine false
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://oss.sonatype.org/content/groups/public/'
  }
}

dependencies {
  api 'net.kyori:adventure-api:4.0.1'
  testImplementation 'com.google.guava:guava-testlib:29.0-jre'
  testImplementation 'com.google.truth:truth:1.0.1'
  testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

jar {
  manifest.attributes(
    'Automatic-Module-Name': 'net.kyori.adventure.text.feature.pagination'
  )
}

inventSonatypeRepository.enabled = System.getenv('GITHUB_ACTIONS') == null || project.version.endsWith('-SNAPSHOT')

publishing {
  publications {
    maven(MavenPublication) {
      from components.java

      artifact javadocJar
      artifact sourcesJar

      pom {
        name = project.name
        url = 'https://github.com/KyoriPowered/adventure-text-feature-pagination'
        description = project.description

        developers {
          developer {
            id = 'kashike'
            timezone = 'America/Vancouver'
          }

          developer {
            id = 'lucko'
            name = 'Luck'
            url = 'https://lucko.me'
            email = 'git@lucko.me'
          }

          developer {
            id = 'zml'
            name = 'zml'
            timezone = 'America/Vancouver'
          }

          developer {
            id = 'Electroid'
          }
        }

        issueManagement {
          system = 'GitHub Issues'
          url = 'https://github.com/KyoriPowered/adventure-text-feature-pagination/issues'
        }

        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/licenses/MIT'
          }
        }

        scm {
          connection = 'scm:git@github.com:KyoriPowered/adventure-text-feature-pagination.git'
          developerConnection = 'scm:git@github.com:KyoriPowered/adventure-text-feature-pagination.git'
          url = 'https://github.com/KyoriPowered/adventure-text-feature-pagination/'
        }
      }
    }
  }
}

signing {
  required { project.hasProperty('signing.keyId') && gradle.taskGraph.hasTask(':publish') && !project.version.endsWith('-SNAPSHOT') }
  sign publishing.publications.maven
}
